#! /bin/zsh

declare -A _bsym
#
# The driver
#
_bsym[_bat]=_bat_$(uname -s)
autoload -Uz $_bsym[_bat]

#
# Colors
#
_bsym[red]='%F{red}'		# "\033[31m"
_bsym[grn]='%F{green}'		# "\033[32m"
_bsym[yel]='%F{yellow}'		# "\033[33m"
_bsym[rst]='%f'			# "\033[00m"

#
# Font Choices
#
_bsym[empty]='\u25AF'		# ▯
_bsym[full_]='\u25Ae'		# ▮
_bsym[acSym]='\u26A1'		# ⚡

#
# alternates
#
_bsym["arrow-empty"]='\u25b9'	# ▹
_bsym["arrow-full_"]='\u25b8'	# ▸
_bsym["block-empty"]='\u25A1'	# □
_bsym["block-full_"]='\u25A0'	# ■

_bsym[bars]=10

function _bcharge {
    local maxCap=$(      $_bsym[_bat] MaxCapacity           )
    local curCap=$(      $_bsym[_bat] CurrentCapacity       )
    local extConn=$(     $_bsym[_bat] ExternalConnected     )
    local extCharging=$( $_bsym[_bat] ExternalChargeCapable )

    local bars=$_bsym[bars]
    local prt=

    # Colour
    charge=$(( bars * ${curCap:-0} / ${maxCap:-1} ))
    if   [[ "$charge" -gt $(( 6 * bars / 10 )) ]]
    then
	prt+="$_bsym[grn]"	#       charge > 60% of bars
    elif [[ $charge -ge $(( 4 * bars / 10 )) ]]
    then
	prt+="$_bsym[yel]"	# 40% < charge < 60%
    else
	prt+="$_bsym[red]"	# 40% < charge
    fi

    # AC
    if [[ "$extConn" = 'Yes' && "$extCharging" = 'Yes' ]] ; then
	prt+="$(printf "%s%s%s" "%{" "$_bsym[acSym]" "%2G%}")"
    fi

    # Bars
    i=0
    while [[ $i -ne "$charge" ]] # fill to charge
    do
	prt+="$_bsym[full_]"
        i=$((i + 1))
    done
    while [[ $i -ne "$bars" ]]	# fill fmt to rest
    do
	prt+="$_bsym[empty]"
        i=$((i + 1))
    done

    # Reset
    prt+="$_bsym[rst]"

    print "$prt"
}

_bcharge
